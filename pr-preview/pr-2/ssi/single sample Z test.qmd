---
draft: false
title: Single sample Z test
date: 2024-01-01
lastmod: 2025-01-01
engine: julia
---
Based on:
Handbook of Parametric and Nonparametric Statistical Procedures, 5th edition, Chapter 1
```{julia}
#| echo: false
#| output: false
using Pkg
Pkg.activate(".")
Pkg.instantiate()
```
```{julia}
#| output: false
using Random
Random.seed!(783376894512)
using Distributions
using Intervals
using Plots
n_sim = 1_000_000 # number of simulations to verify theoretical distributions
```
## True model
```{julia}
#| output: false
n = 3
σₜ = 2.0 # assumed known
μₜ = 3.0
Mₜ = Product([Normal(μₜ, σₜ) for _ in 1:n]) # assumed known to be Normal and i.i.d.
generate_data(Mₜ) = rand(Mₜ)
```
## Hypothesis testing
```{julia}
#| output: false
function test_statistic(y, μ₀, σₜ)
    n = length(y)
    μ̂ = sum(y) / n
    return (μ̂ - μ₀) / (σₜ / √n)
end
```
### Null Hypothesis is true
```{julia}
#| output: false
μ₀ = 3.0
μₜ == μ₀

simulated_statistics = [test_statistic(generate_data(Mₜ), μ₀, σₜ) for _ in 1:n_sim]
distribution_under_H₀ = z -> pdf(Normal(), z)
```
```{julia}
#| code-fold: true
histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₀, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n when Null Hypothesis is correct",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
### Null Hypothesis is true, but assumptions don't hold
#### (Known) variance is wrong
```{julia}
#| output: false
σ_wrong = 3.0
M_wrong_variance = Product([Normal(μₜ, σ_wrong) for _ in 1:n])
σ_wrong != σₜ

simulated_statistics = [test_statistic(generate_data(M_wrong_variance), μ₀, σₜ) for _ in 1:n_sim]
```
```{julia}
#| code-fold: true
histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₀, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₀, but variance is wrong",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
#### Observations are non-normal 
```{julia}
#| output: false
M_non_normal =  Product([MixtureModel([Normal(μₜ+sqrt(3.99), 0.1),
									   Normal(μₜ-sqrt(3.99), 0.1),], [0.5, 0.5]) for _ in 1:n])
all(mean(M_non_normal) .== μₜ)
all(sqrt.(var(M_non_normal)) .≈ σₜ)

simulated_statistics = [test_statistic(generate_data(M_non_normal), μ₀, σₜ) for _ in 1:n_sim]
```
```{julia}
#| code-fold: true
histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₀, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₀, but non-normal observations",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
#### Observations are non-independent
```{julia}
#| output: false
M_non_independent = MvNormal([μₜ, μₜ, μₜ], [σₜ^2 0.5*σₜ^2 0; 0.5*σₜ^2 σₜ^2 0; 0 0 σₜ^2])
all(mean(M_non_independent) .== μₜ)
all(sqrt.(var(M_non_independent )) .≈ σₜ)

simulated_statistics = [test_statistic(generate_data(M_non_independent), μ₀, σₜ) for _ in 1:n_sim]
```
```{julia}
#| code-fold: true
histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₀, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₀, but non-independent observations",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
#### Observations are non-identical
There is no way of violating the identical distribution assumption,
without also violating another assumption already present in the previous 3 counterexamples.

### Null Hypothesis is false
```{julia}
#| output: false
μ₀ = 5.0
μₜ != μ₀

simulated_statistics = [test_statistic(generate_data(Mₜ), μ₀, σₜ) for _ in 1:n_sim]
distribution_under_H₁ = znc -> pdf(Normal((μₜ - μ₀) / (σₜ / √n)), znc)
```
```{julia}
#| code-fold: true
histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₁, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n when Null Hypothesis is incorrect",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
### Null Hypothesis is false, but assumptions don't hold
Same counter examples as above.
```{julia}
#| code-fold: true
simulated_statistics = [test_statistic(generate_data(M_wrong_variance), μ₀, σₜ) for _ in 1:n_sim]

histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₁, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₁, but variance is wrong",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
```{julia}
#| code-fold: true
simulated_statistics = [test_statistic(generate_data(M_non_normal), μ₀, σₜ) for _ in 1:n_sim]

histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₁, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₁, but non-normal observations",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
```{julia}
#| code-fold: true
simulated_statistics = [test_statistic(generate_data(M_non_independent), μ₀, σₜ) for _ in 1:n_sim]

histogram(simulated_statistics; normalize=true, label="Simulated")
zmin, zmax = minimum(simulated_statistics), maximum(simulated_statistics)
z_plot = zmin:((zmax - zmin) / 100):zmax
plot!(distribution_under_H₁, z_plot; lw=5, label="Theoretical")
plot!(;
    title="Distribution of single sample Z test statistic, \n under H₁, but non-independent observations",
    xlimits=(zmin, zmax),
    xlabel="z",
    ylabel="pdf",
    grid=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
# Confidence Interval
```{julia}
#| output: false
α = 0.05 # proportion of CIs which should fail to cover true values
function confidence_interval(y, σₜ, α)
    n = length(y)
    μ̂ = sum(y) / n
    L = μ̂ + quantile(Normal(), α / 2) * (σₜ / √n)
    U = μ̂ + quantile(Normal(), 1 - α / 2) * (σₜ / √n)
    return Interval(L, U)
end
```
```{julia}
#| output: false
simulated_CIs = [confidence_interval(generate_data(Mₜ), σₜ, α) for _ in 1:n_sim]
coverage = count([μₜ in CI for CI in simulated_CIs]) / n_sim
```
```{julia}
#| code-fold: true
bar(["inside CI", "outside CI"], [coverage, 1 - coverage])
plot!(;
    title="Simulated Coverage of 95% confidence interval \n corresponding to single sample Z test",
    ylimits=(0.0, 1.0),
    yticks=[0.05, 0.95],
    ylabel="proportion",
    grid=true,
    legend=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
## assumptions don't hold
Same counter examples as above.
```{julia}
#| code-fold: true
simulated_CIs = [confidence_interval(generate_data(M_wrong_variance), σₜ, α) for _ in 1:n_sim]
coverage = count([μₜ in CI for CI in simulated_CIs]) / n_sim

bar(["inside CI", "outside CI"], [coverage, 1 - coverage])
plot!(;
    title="Simulated Coverage of 95% confidence interval \n corresponding to single sample Z test \n but variance is wrong",
    ylimits=(0.0, 1.0),
    yticks=[0.05, 0.95],
    ylabel="proportion",
    grid=true,
    legend=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```
```{julia}
#| code-fold: true
simulated_CIs = [confidence_interval(generate_data(M_non_normal), σₜ, α) for _ in 1:n_sim]
coverage = count([μₜ in CI for CI in simulated_CIs]) / n_sim

bar(["inside CI", "outside CI"], [coverage, 1 - coverage])
plot!(;
    title="Simulated Coverage of 95% confidence interval \n corresponding to single sample Z test \n but non-normal observations",
    ylimits=(0.0, 1.0),
    yticks=[0.05, 0.95],
    ylabel="proportion",
    grid=true,
    legend=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
    top_margin = 25Plots.px,
)
```
```{julia}
#| code-fold: true
simulated_CIs = [confidence_interval(generate_data(M_non_independent), σₜ, α) for _ in 1:n_sim]
coverage = count([μₜ in CI for CI in simulated_CIs]) / n_sim

bar(["inside CI", "outside CI"], [coverage, 1 - coverage])
plot!(;
    title="Simulated Coverage of 95% confidence interval \n corresponding to single sample Z test \n but non-independent observations",
    ylimits=(0.0, 1.0),
    yticks=[0.05, 0.95],
    ylabel="proportion",
    grid=true,
    legend=false,
    legendfontsize=12,
    tickfontsize=12,
    guidefontsize=16,
)
```